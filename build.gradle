plugins {
    id 'com.palantir.docker' version '0.25.0' apply false
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id 'com.github.harbby.gradle.serviceloader' version '1.1.5'
    id "org.owasp.dependencycheck" version "7.2.0"
}

dependencyCheck {
    format='HTML'
    failBuildOnCVSS=5
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.palantir.docker'

ext {
    sharedDir = file("${project.rootDir}/shared")
    sailfishVersion = '3.3.54'
    commonVersion   = '5.1.0-dev-version-5+'
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

ext.excludeSailfish = { rcd ->
    rcd.excludeModule("com.exactpro.sf", "sailfish-core")
    rcd.excludeModule("com.exactpro.sf", "sailfish-common")
    rcd.excludeModule("com.exactpro.sf", "sailfish-rest-api-client")
    rcd.excludeModule("com.exactpro.sf", "service-http")
}

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }

    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        content {
            excludeSailfish(it)
        }
    }

    // ignoreGradleMetadataRedirection is used for sonatype because
    // Sailfish dependencies have constrains that interfere with our BOM
    // so we exclude Gradle metadata for this repositories.
    // We've checked these versions - they are compatible and safe to use
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        content {
            excludeSailfish(it)
        }
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }
    mavenCentral()
    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:4.1.0') 

    implementation "com.exactpro.th2:common:${commonVersion}"
    
    implementation 'com.exactpro.th2:codec:5.0.0-TH2-4262-4085740155-cf2c1e3-SNAPSHOT'
    implementation 'com.exactpro.th2:sailfish-utils:3.14.0'

    implementation "com.exactpro.sf:service-http:${sailfishVersion}"

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: '2.0.10'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: kotlin_version

    testImplementation testFixtures("com.exactpro.th2:common:${commonVersion}")
}

test {
    useJUnitPlatform()
}

application {
    mainClassName 'com.exactpro.th2.codec.MainKt'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

serviceLoader {
    serviceInterface 'com.exactpro.th2.codec.api.IPipelineCodecFactory'
}
